plugins {
  id "com.android.application"
  id "kotlin-android"
  id "kotlin-kapt"
  id "kotlin-parcelize"
  id "androidx.navigation.safeargs.kotlin"
  id "com.google.dagger.hilt.android"
}

android {
  namespace "jp.co.yumemi.android.codecheck"
  compileSdk 33

  defaultConfig {
    applicationId "jp.co.yumemi.android.codecheck"
    minSdk 23
    targetSdk 33
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_17.toString()
  }
  buildFeatures {
    viewBinding true
  }
  lintOptions {
    xmlReport true
  }
}

kapt {
  correctErrorTypes true
}

configurations {
  ktlint
}

dependencies {
  implementation "androidx.core:core-ktx:1.10.1"
  implementation "androidx.appcompat:appcompat:1.6.1"
  implementation "androidx.constraintlayout:constraintlayout:2.1.4"
  implementation "androidx.recyclerview:recyclerview:1.3.0"
  implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"
  implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"
  implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
  implementation "androidx.navigation:navigation-fragment-ktx:2.6.0"
  implementation "androidx.navigation:navigation-ui-ktx:2.6.0"
  implementation "com.google.android.material:material:1.9.0"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1"
  implementation "io.ktor:ktor-client-android:2.3.1"
  implementation "io.coil-kt:coil:2.4.0"
  implementation "com.google.dagger:hilt-android:2.46.1"
  kapt "com.google.dagger:hilt-compiler:2.46.1"
  testImplementation "junit:junit:4.13.2"
  ktlint "com.pinterest:ktlint:0.49.1"
}

task ktlint(type: JavaExec, group: "verification") {
  description = "Check Kotlin code style."
  classpath = configurations.ktlint
  main = "com.pinterest.ktlint.Main"
  args "src/**/*.kt", "--color", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
  ignoreExitValue true
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
  description = "Fix Kotlin code style deviations."
  classpath = configurations.ktlint
  main = "com.pinterest.ktlint.Main"
  args "-F", "src/**/*.kt"
  jvmArgs("--add-opens", "java.base/java.lang=ALL-UNNAMED")
  ignoreExitValue true
}
